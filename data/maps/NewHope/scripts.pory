raw `
NewHope_MapScripts::
    map_script MAP_SCRIPT_ON_RESUME, NewHope_OnResume
	map_script MAP_SCRIPT_ON_FRAME_TABLE, NewHope_OnFrame
	.byte 0

NewHope_OnFrame:
	map_script_2 MET_RIVAL, 0, NewHope_EventScript_Intro
    map_script_2 VAR_STARTER_MON, 1, NewHope_EventScript_Mentor
	.2byte 0
`

script NewHope_OnResume {
    if (flag(FLAG_VGC_PENDING_RESTORE)) {
        special(Vgc_RestorePartyBackup)
        special(HealPlayerParty)
        callnative(UpdateFollowingPokemon)
        special(Vgc_RestorePartyBackup)
        special(WeightedSelector_Off)
        clearflag(FLAG_VGC_PENDING_RESTORE)
    }  
    end
}

script NewHope_EventScript_WarpPV{
    lockall
    faceplayer
    msgbox("This is the warp to Port Valor", MSGBOX_AUTOCLOSE)
    releaseall
    end
}


//  ============= NPC SCRIPTS =============

// Meeting RIVAL and exploring New Hope
script NewHope_EventScript_Intro {
    setvar(MET_RIVAL, 1)
    setflag(FLAG_NEWHOPE_MENTOR)
    setflag(FLAG_GOON1_1ST_CONTACT)
    setflag(FLAG_GOON2_1ST_CONTACT)
    setflag(FLAG_FIRST_RIVAL_BATTLE)
    lockall
    applymovement(LOCALID_RIVAL, npc_exclaim)
    waitmovement(0)
    turnobject(LOCALID_RIVAL, DIR_NORTH)
    msgbox("RIVAL: Oh! Hey, {PLAYER}!", MSGBOX_AUTOCLOSE)
    msgbox("I was just coming to see you.", MSGBOX_AUTOCLOSE)
    msgbox("You're going to see Professor Cinder?", MSGBOX_AUTOCLOSE)
    msgbox("Come on, I'll walk with you.", MSGBOX_AUTOCLOSE)
    closemessage
    delay(16)

    applymovement(LOCALID_RIVAL, RIVAL_to_LeagueHQ)
    applymovement(OBJ_EVENT_ID_PLAYER, Player_to_LeagueHQ)
    waitmovement(0)
    turnobject(LOCALID_RIVAL, DIR_NORTH)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)

    msgbox("RIVAL: Is your dad at the League\nHeadquarters today?", MSGBOX_AUTOCLOSE)
    msgbox("No? Well, I'm sure you'll see him soon\nenough.", MSGBOX_AUTOCLOSE)

    applymovement(LOCALID_RIVAL, RIVAL_to_Cinder)
    applymovement(OBJ_EVENT_ID_PLAYER, Player_to_Cinder)
    waitmovement(0)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)

    msgbox("RIVAL: Here we are.", MSGBOX_AUTOCLOSE)
    msgbox("I got my POKEMON from Cinder the other\nday. I already have three now.", MSGBOX_AUTOCLOSE)
    msgbox("Maybe we'll battle once you have a few\nmore.", MSGBOX_AUTOCLOSE)
    msgbox("I'm going to go train so I'm ready in\ncase those Galarians ever want\pmore trouble.", MSGBOX_AUTOCLOSE)

    applymovement(LOCALID_RIVAL, RIVAL_to_Cinder)
    waitmovement(0)
    setflag(FLAG_NEWHOPE_RIVAL)

    releaseall
    end
}

// Meeting MENTOR after getting starter
script NewHope_EventScript_Mentor {
    setvar(VAR_STARTER_MON, 0)
    clearflag(FLAG_NEWHOPE_MENTOR)
    addobject(LOCALID_MENTOR)
    lockall
    applymovement(LOCALID_MENTOR, Mentor_Start)
    waitmovement(0)
    turnobject(LOCALID_MENTOR, DIR_EAST)
    delay(8)
    applymovement(LOCALID_MENTOR, npc_exclaim)
    waitmovement(0)
    turnobject(LOCALID_MENTOR, DIR_EAST)
    delay(8)
    applymovement(LOCALID_MENTOR, Mentor_to_Player)
    waitmovement(0)
    turnobject(LOCALID_MENTOR, DIR_EAST)

    msgbox("MENTOR: Oh! Hey, {PLAYER}!", MSGBOX_AUTOCLOSE)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
    msgbox("Did you just get your first POKEMON from\nthe professor?", MSGBOX_AUTOCLOSE)
    delay(8)
    msgbox("Aww! It's so cute!", MSGBOX_AUTOCLOSE)
    msgbox("My dad gave me a Pikachu for my birthday.", MSGBOX_AUTOCLOSE)
    delay(60)
    applymovement(LOCALID_MENTOR, npc_exclaim)
    waitmovement(0)
    turnobject(LOCALID_MENTOR, DIR_EAST)
    delay(60)
    msgbox("What?! No! You know I don't want to\nbattle with her!", MSGBOX_AUTOCLOSE)
    msgbox("Why is everyone in this region so\nobsessed with competition?", MSGBOX_AUTOCLOSE)
    msgbox("We weren't even alive for the war!", MSGBOX_AUTOCLOSE)
    msgbox("I hope you wise up one day and realize\nthere's more to POKEMON than battling.", MSGBOX_AUTOCLOSE)
    delay(16)
    msgbox("Whatever. I'll see you later.", MSGBOX_AUTOCLOSE)
    applymovement(LOCALID_MENTOR, Mentor_Leave)
    waitmovement(0)
    setflag(FLAG_NEWHOPE_MENTOR)
    removeobject(LOCALID_MENTOR)
    releaseall
    end
}

script NewHope_EventScript_Villager1 {
    lockall
    faceplayer
    giveitem(ITEM_TM45)

    msgbox("Would you like rare candies?", MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES) {
        giveitem(ITEM_RARE_CANDY, 50)
    }

    msgbox("Ability items?", MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES) {
        giveitem(ITEM_ABILITY_CAPSULE, 5)
        giveitem(ITEM_ABILITY_PATCH, 5)
    }

    msgbox("Would you like a badge?", MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES) {
        if (var(VAR_BADGES) < 8) {
            addvar(VAR_BADGES, 1)
            msgbox("Badge acquired", MSGBOX_AUTOCLOSE)
        } else {
            msgbox("Max badges reached", MSGBOX_AUTOCLOSE)
        }

        if (var(VAR_LEVEL_CAP) == 21){
            setvar(VAR_LEVEL_CAP, 29)
            msgbox("Level cap increased to 29", MSGBOX_AUTOCLOSE)
        } elif (var(VAR_LEVEL_CAP) == 29) {
            setvar(VAR_LEVEL_CAP, 41)
            msgbox("Level cap increased to 41", MSGBOX_AUTOCLOSE)
        } elif (var(VAR_LEVEL_CAP) == 41) {
            setvar(VAR_LEVEL_CAP, 49)
            msgbox("Level cap increased to 49", MSGBOX_AUTOCLOSE)
        } elif (var(VAR_LEVEL_CAP) == 49) {
            setvar(VAR_LEVEL_CAP, 56)
            msgbox("Level cap increased to 56", MSGBOX_AUTOCLOSE)
        } elif (var(VAR_LEVEL_CAP) == 56) {
            setvar(VAR_LEVEL_CAP, 64)
            msgbox("Level cap increased to 64", MSGBOX_AUTOCLOSE)
        } elif (var(VAR_LEVEL_CAP) == 64) {
            setvar(VAR_LEVEL_CAP, 71)
            msgbox("Level cap increased to 71", MSGBOX_AUTOCLOSE)
        } elif (var(VAR_LEVEL_CAP) == 71) {
            setvar(VAR_LEVEL_CAP, 78)
            msgbox("Level cap increased to 78", MSGBOX_AUTOCLOSE)
        } elif (var(VAR_LEVEL_CAP) == 78) {
            setvar(VAR_LEVEL_CAP, 85)
            msgbox("Level cap increased to 85", MSGBOX_AUTOCLOSE)
        } else {
            msgbox("Max level cap reached.", MSGBOX_AUTOCLOSE)
        }
    }

    msgbox("Reset badges?", MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES) {
        setvar(VAR_BADGES, 0)
        setvar(VAR_LEVEL_CAP, 21)
        msgbox("Badges and lvl cap reset", MSGBOX_AUTOCLOSE)
    }

    releaseall
    end
}

script NewHope_EventScript_Security {
    lockall
    faceplayer
    msgbox("GUARD: I'm the security guard for the\nLeague Headquarters.", MSGBOX_AUTOCLOSE)
    msgbox("If you don't have League business, you\nbetter move along.", MSGBOX_AUTOCLOSE)
    turnobject(LOCALID_NH_SECURITY, DIR_SOUTH)
    releaseall
    end
}


//  ============= TRIGGERS =============

script NewHope_Trigger_BlockPath{
    if (!(flag(STARTER_BALL_ONE) || flag(STARTER_BALL_TWO) || flag(STARTER_BALL_THREE)) && !(flag(FLAG_LEAGUE_REG))) {
        lockall
        msgbox("You need a pokemon and to register with\nthe League office.")
        applymovement(OBJ_EVENT_ID_PLAYER, StopPlayer_Up)
        waitmovement(0)
        releaseall
    }
    elif (!(flag(STARTER_BALL_ONE) || flag(STARTER_BALL_TWO) || flag(STARTER_BALL_THREE))) {
        lockall
        msgbox("You need a pokemon first.")
        applymovement(OBJ_EVENT_ID_PLAYER, StopPlayer_Up)
        waitmovement(0)
        releaseall
    }
    elif (!(flag(FLAG_LEAGUE_REG))) {
        lockall
        msgbox("You need to register with the league\nfirst.")
        applymovement(OBJ_EVENT_ID_PLAYER, StopPlayer_Up)
        waitmovement(0)
        releaseall
    }   
}

script NewHope_EventScript_GymTrigger {
    if ((var(VAR_BADGES) == 0)) {
        msgbox("This is where the rival battle will go.", MSGBOX_AUTOCLOSE)
        closemessage
    } elif (var(VAR_BADGES) == 2) {
        lockall
        clearflag(FLAG_GOON1_1ST_CONTACT)
        addobject(LOCALID_NH_GOON1)
        applymovement(OBJ_EVENT_ID_PLAYER, Move_Player_for_Goons)
        turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
        waitmovement(0)
        applymovement(LOCALID_NH_GOON1, Goon1_Down)
        waitmovement(0)
        turnobject(LOCALID_NH_GOON1, DIR_EAST)
        clearflag(FLAG_GOON2_1ST_CONTACT)
        addobject(LOCALID_NH_GOON2)
        applymovement(LOCALID_NH_GOON2, Goon2_Down)
        waitmovement(0)

        msgbox("GOON 1: I can't believe they wouldn't\nlisten.", MSGBOX_AUTOCLOSE)
        msgbox("GOON 2: The Champion must have gotten\nto them first.\pLet's try the next gym.", MSGBOX_AUTOCLOSE)

        applymovement(LOCALID_NH_GOON1, Goons_Exit_Down)
        applymovement(LOCALID_NH_GOON2, Goons_Exit_Down)
        waitmovement(0)
        setflag(FLAG_GOON1_1ST_CONTACT)
        removeobject(LOCALID_NH_GOON1)
        setflag(FLAG_GOON2_1ST_CONTACT)
        removeobject(LOCALID_NH_GOON2)
        
    } elif (var(VAR_BADGES) == 4) {
        msgbox("This is where the second PT encounter will go.", MSGBOX_AUTOCLOSE)
        closemessage
    }

    releaseall
    end
}


//  ============= MOVEMENTS =============

// Intro cutscene movements
movement npc_exclaim {
    emote_exclamation_mark
    end
}

movement RIVAL_to_LeagueHQ {
    walk_down
    walk_down
    walk_right * 16
    end
}

movement Player_to_LeagueHQ {
    walk_down
    walk_down
    walk_down
    walk_down
    walk_right * 15
    end
}

movement RIVAL_to_Cinder {
    walk_down * 16
    walk_left * 12
    end
}

movement Player_to_Cinder {
    walk_down * 17
    walk_left * 12
    end
}

movement RIVAL_Leave {
    walk_right * 10
    end
}

// Movements for meeting mentor
movement Mentor_Start {
    walk_right
    walk_right
    walk_right
    walk_right
    walk_right
    face_right
    end
}

movement Mentor_to_Player {
    walk_right
    walk_right
    face_right
    end
}

movement Mentor_Leave {
    walk_down
    walk_right * 10
    end
}

// Movements for Gym trigger
movement Move_Player_for_Goons {
    walk_fast_right
    end
}

movement Goon1_Down {
    walk_down
    walk_left
    end
}

movement Goon2_Down {
    walk_down
    end
}

movement Goons_Exit_Down {
    walk_down * 5
    walk_right * 5
    walk_down * 5
    end
}

// Movement for triggers on edge of town
movement StopPlayer_Up{
    walk_up
}